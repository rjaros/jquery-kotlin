buildscript {
    ext.production = (findProperty('prod') ?: 'false') == 'true'
    ext.kotlin_version = "1.3.11"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin-platform-js'
apply from: rootProject.file('pom.gradle')

group = 'pl.treksoft'
version = '0.0.4'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task cleanLibs(type: Delete) {
    delete 'build/classes', 'build/libs'
}

if (project.gradle.startParameter.taskNames.contains("jar")) {
    compileKotlin2Js.dependsOn 'cleanLibs'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = !production
    kotlinOptions.moduleKind = 'umd'
}

publishing {
    publications {
        mavenProject(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourceJar {
                classifier 'sources'
            }

            withPom(pom)
        }
    }
}

model {
    tasks.generatePomFileForMavenProjectPublication {
        destination = file("$buildDir/libs/${project.name}-${version}.pom")
    }
}


bintray {
    user = findProperty('buser')
    key = findProperty('bkey')
    pkg {
        repo = 'kotlin'
        name = "${project.name}"
        licenses = ['MIT']
        vcsUrl = "https://github.com/rjaros/${project.name}.git"
        version {
            name = "${project.version}"
            desc = 'Kotlin definition files for the jQuery library.'
            released = new Date()
        }
    }
    filesSpec {
        from "${project.buildDir}/libs/"
        into "pl/treksoft/${project.name}/${project.version}"
    }
}

bintrayUpload {
    dependsOn "generatePomFileForMavenProjectPublication"
}
